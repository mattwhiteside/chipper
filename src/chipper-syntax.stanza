#if-defined(STANDALONE-CHIPPER) :
   #include<"core/stringeater.stanza">
   #include<"core/macro-utils.stanza">
   #include<"compiler/stz-params.stanza">
   #include<"compiler/stz-algorithms.stanza">
   #include<"compiler/stz-lexer.stanza">
   #include<"compiler/stz-parser.stanza">
   #include<"compiler/stz-core-macros.stanza">

defpackage chipper/syntax :
   import core
   import verse
   import core/macro-utils
   import stz/lexer

;================ CONVENIENCE FUNCTIONS =====================
defn first-info? (form) -> FileInfo|False :
   match(form) :
      (form:Token) : info(form)
      (form:List) : search(first-info?, form)
      (form) : false

defn first-info (form:List) :
   match(first-info?(form)) :
      (i:FileInfo) : i
      (f:False) : FileInfo()

defn CPE (form, x) :
   throw $ new Exception :
      defmethod print (o:OutputStream, this) :
         print(o, "[~] Chipper Parsing Error: ~" << [first-info(form), x])

;=============== CHIPPER PARSE STRUCTURES ==================
definterface Type
defstruct UIntT <: Type
defstruct UIntWT <: Type : (width)
defstruct SIntT <: Type
defstruct SIntWT <: Type : (width)
defstruct VecT <: Type : (type:Type, length)
defstruct NamedT <: Type : (name, targs:List<Type>, args:List)
defstruct ParamT <: Type : (name)
defstruct EscapeT <: Type : (exp)

defstruct TArg : (inherit:True|False, name, bound)
defstruct Field : (inherit:True|False, flip:True|False, name, type:Type)
defstruct DefBundle : (name, parent, targs:List<TArg>, arglist:List, fields:List<Field>)
defstruct Port : (inherit:True|False, input?:True|False, name, type:Type)
defstruct Param : (inherit:True|False, name, type, exp)
defstruct DefModule : (name, parent, targs:List<TArg>, arglist:List, ports:List<Port>, params:List<Param>, body:List)
defstruct RegisterStmt : (name, type:Type)
defstruct RegisterFromExp : (name, exp)
defstruct RegisterResetFromExp : (name, exp)
defstruct RegisterExp : (type:Type)
defstruct NodeStmt : (name, x)
defstruct CMemStmt : (name, type:Type)
defstruct SMemStmt : (name, type:Type)
defstruct WireStmt : (name, type:Type)
defstruct WireFromExp : (name, exp)
defstruct WireExp : (type:Type)
defstruct InstanceStmt : (name, module:Type)
defstruct InstanceExp : (module:Type)
defstruct CircuitStmt : (name, module:Type)
defstruct WhenStmt : (pred, conseq, alt)
defstruct ConnectStmt : (x, y)
defstruct OnResetStmt : (x, y)
;============================================================

;============ DEBUG: PRINTING ===============================
defn commas (xs) : join(xs, ", ")

defmethod print (o:OutputStream, t:TArg) :
   print(o, "~~ <: ~" << [
      "inherit " when inherit(t) else ""
      name(t),
      bound(t)])

defmethod print (o:OutputStream, t:Type) :
   print{o, _} $ match(t) :
      (t:UIntT) : "UInt"
      (t:UIntWT) : "UInt<~>" << [width(t)]
      (t:SIntT) : "SInt"
      (t:SIntWT) : "SInt<~>" << [width(t)]
      (t:VecT) : "~[~]" << [type(t), length(t)]
      (t:NamedT) : "~<~@>(~@)" << [name(t), commas(targs(t)), commas(args(t))]
      (t:ParamT) : name(t)
      (t:EscapeT) : "#{~}" << [exp(t)]

defmethod print (o:OutputStream, f:Field) :
   print(o, "~~~ : ~" << [
      "inherit " when inherit(f) else ""
      "flip " when flip(f) else ""
      name(f)
      type(f)])

defmethod print (o:OutputStream, b:DefBundle) :
   print(o, "defbundle ~<~@> <: ~ ~ :" << [name(b), commas(targs(b)), parent(b), arglist(b)])
   for f in fields(b) do : print(o, "\n   ~" << [f])

defmethod print (o:OutputStream, p:Port) :
   print(o, "~~ ~ : ~" << [
      "inherit " when inherit(p) else ""
      "input" when input?(p) else "output"
      name(p)
      type(p)])

defmethod print (o:OutputStream, m:DefModule) :
   print(o, "defmodule ~<~@> ~ ~ :" << [name(m), targs(m), parent(m), arglist(m)])
   for p in ports(m) do : print(o, "\n   ~" << [p])
   print(o, "\n   ~@" << [join(body(m), " ")])

defmethod print (o:OutputStream, s:RegisterStmt) :
   print(o, "reg ~ : ~" << [name(s), type(s)])

defmethod print (o:OutputStream, s:NodeStmt) :
   print(o, "node ~ : ~" << [name(s), x(s)])

defmethod print (o:OutputStream, s:CMemStmt) :
   print(o, "cmem ~ : ~" << [name(s), type(s)])

defmethod print (o:OutputStream, s:SMemStmt) :
   print(o, "smem ~ : ~" << [name(s), type(s)])

defmethod print (o:OutputStream, s:WireStmt) :
   print(o, "wire ~ : ~" << [name(s), type(s)])

defmethod print (o:OutputStream, s:InstanceStmt) :
   print(o, "inst ~ of ~" << [name(s), module(s)])

defmethod print (o:OutputStream, s:RegisterExp) :
   print(o, "regof ~" << [type(s)])

defmethod print (o:OutputStream, s:WireExp) :
   print(o, "wireof ~" << [type(s)])

defmethod print (o:OutputStream, s:InstanceExp) :
   print(o, "instof ~" << [module(s)])

defmethod print (o:OutputStream, s:CircuitStmt) :
   print(o, "circuit ~ of ~" << [name(s), module(s)])

defmethod print (o:OutputStream, s:WhenStmt) :
   val io = IndentedStream(o, 3)
   print(o, "when ~ :" << [pred(s)])
   print(io, "\n~" << [conseq(s)])
   print(o, "\nelse :")
   print(io, "\n~" << [alt(s)])

defmethod print (o:OutputStream, s:ConnectStmt) :
   print(o, "~ := ~" << [x(s), y(s)])

defmethod print (o:OutputStream, s:OnResetStmt) :
   print(o, "on-reset ~ := ~" << [x(s), y(s)])
;============================================================

;================= CHIPPER PARSER ===========================
defn* apply-suffix-ops (t, fs:List) :
   if empty?(fs) : t
   else : apply-suffix-ops(head(fs)(t), tail(fs))

defn name? (x) :
   match(unwrap-token(x)) :
      (x:Symbol) :         
         {not contains?(_, x)} $ `(
            & | : . <:
            val var fn label for)
      (x) : false

defsyntax chipper :
   ;Holds the current type parameters in scope
   var TYPE-PARAMETERS:List<TArg> = List()
   defn add-params (ts:List<TArg>) :
      append(stream(unwrap-token, ts), TYPE-PARAMETERS)
   defn tparam? (x) :
      val x* = unwrap-token(x)
      for v in TYPE-PARAMETERS any? :
         unwrap-token(name(v)) == x*

   ;Chipper Error Handling
   defrule :
      ch/type! = (?t:#ch/type) : t
      ch/type! != () : CPE(form, "Chipper type expected here.")
      ch/type$ = (?t:#ch/type! ?rest ...) :
         if empty?(rest) : t
         else : CPE(form, "Single chipper type expected here.")

      ch/name = (?x) when name?(x) : unwrap-token(x)
      ch/name! = (?x:#ch/name) : x
      ch/name! != () : error("Not a name: ~" % [form])

   ;Chipper Types
   defrule :
      symbol = (?x) when unwrap-token(x) typeof Symbol : x
      ch/ts = ((@of ?ts:#ch/type! ...)) : ts
      ch/ts = () : List()
      ch/es = ((@do ?es:#exp! ...)) : es
      ch/es = () : List()

      ch/type = (?t:#ch/term ?ops:#ch/op ...) : apply-suffix-ops(t, ops)
      ch/op = ((@get ?n:#exp$)) : (fn (t) : VecT(t, n))
      ch/term = (?x) when tparam?(x) : ParamT(x)
      ch/term = (typeof<?>(?e:#exp$)) : EscapeT(qquote(chipper/erase-width(chipper/type(~ e))))
      ch/term = (typeof(?e:#exp$)) : EscapeT(qquote(chipper/type(~ e)))
      ch/term = (~ #{?e:#exp$}) : EscapeT(e)
      ch/term = (Bits<?e:#exp$>) : UIntWT(e)
      ch/term = (Bits) : UIntT()
      ch/term = (UInt<?e:#exp$>) : UIntWT(e)
      ch/term = (UInt) : UIntT()
      ch/term = (SInt<?e:#exp$>) : SIntWT(e)
      ch/term = (SInt) : SIntT()
      ch/term = (?x:#symbol ?ts:#ch/ts ?es:#ch/es) : NamedT(x, ts, es)

   ;Helper Rules
   defrule :
      ch/inherit = (inherit) : true
      ch/inherit = () : false
      ch/targ = (?i:#ch/inherit ?t:#ch/name! & ?b:#type!) : TArg(i, t, b)
      ch/targ = (?i:#ch/inherit ?t:#ch/name!) : TArg(i, t, `chipper/Data)
      ch/targs = ((@of ?ts:#ch/targ ...)) : ts
      ch/targs = () : List()
      ch/arglist = ((?arglist ...)) : arglist
      ch/arglist = () : List()
      ch/parent = (<: ?p:#type!) : p
      ch/parent = () : `chipper/Data

   ;Bundle Definition
   defrule :
      ch/field = (?i:#ch/inherit flip ?name:#ch/name! : ?t:#ch/type!) : Field(i, true, name, t)
      ch/field = (?i:#ch/inherit ?name:#id! : ?t:#ch/type!) : Field(i, false, name, t)
      prefix-op = (defbundle ?bname:#id! ?ts:#ch/targs ?p:#ch/parent ?arglist:#ch/arglist :
                      ?body ...) :
         let-var TYPE-PARAMETERS = add-params(ts) :              
            match-syntax(body) :
               (?fs:#ch/field ...) :
                  for f in fs do :
                     if not statically-known?(type(f)) :
                        CPE(form, "Field ~ does not have a statically known type." % [name(f)])
                  compile(DefBundle(bname, p, ts, arglist, fs))

   ;Module Definition
   defrule :
      ch/port = (?i:#ch/inherit input ?name:#id! : ?t:#ch/type!) : Port(i, true, name, t)
      ch/port = (?i:#ch/inherit output ?name:#id! : ?t:#ch/type!) : Port(i, false, name, t)
      ch/param = (?i:#ch/inherit param ?name:#id! : ?t:#type! = ?e:#exp!) : Param(i, name, t, e)
      prefix-op = (defmodule ?mname:#id! ?ts:#ch/targs ?p:#ch/parent ?arglist:#ch/arglist :
                      ?body ...) :
         let-var TYPE-PARAMETERS = add-params(ts) :
            match-syntax(body) :
               (?ports:#ch/port ... ?params:#ch/param ... ?stmts:#exps!) :
                  for p in ports do :
                     if not statically-known?(type(p)) :
                        CPE(form, "Port ~ does not have a statically known type." % [name(p)])
                  compile(DefModule(mname, p, ts, arglist, ports, params, stmts))

   ;Register statement
   defrule :
      prefix-op = (reg ?name:#id! := ?e:#exp!) :
         compile(RegisterFromExp(name, e))
      prefix-op = (reg ?name:#id! : ?type:#ch/type! := ?e:#exp!) :
         expande $ list $ list(
            compile(RegisterStmt(name, type))
            compile(ConnectStmt(name, e)))
      prefix-op = (reg ?name:#id! : ?type:#ch/type = ?e:#exp!) :
         expande $ list $ list(
            compile(RegisterStmt(name, type))
            compile(OnResetStmt(name, e)))
      prefix-op = (reg ?name:#id! = ?e:#exp!) :
         compile(RegisterResetFromExp(name, e))
      prefix-op = (reg ?name:#id! : ?type:#ch/type!) :
         compile(RegisterStmt(name, type))
      prefix-op = (regof ?type:#ch/type!) :
         compile(RegisterExp(type))

   ;Node statement
   defrule :
      prefix-op = (node ?name:#id! = ?x:#exp!) :
         compile(NodeStmt(name, x))

   ;Mem statement
   defrule :
      prefix-op = (cmem ?name:#id! : ?type:#ch/type!) :
         compile(CMemStmt(name, type))
      prefix-op = (smem ?name:#id! : ?type:#ch/type!) :
         compile(SMemStmt(name, type))

   ;Wire statement
   defrule :
      prefix-op = (wire ?name:#id! := ?e:#exp!) :
         compile(WireFromExp(name, e))
      prefix-op = (wire ?name:#id! : ?type:#ch/type! := ?e:#exp!) :
         expande $ list $ list(
            compile(WireStmt(name, type))
            compile(ConnectStmt(name, e)))
      prefix-op = (wire ?name:#id! : ?type:#ch/type!) :
         compile(WireStmt(name, type))
      prefix-op = (wireof ?type:#ch/type!) :
         compile(WireExp(type))
      prefix-op = (wire ?name:#id! = ?e:#exp!) :
         compile(NodeStmt(name, e))

   ;Instance Statement
   defrule :
      prefix-op = (inst ?name:#id! of ?module:#ch/type!) :
         compile(InstanceStmt(name, module))
      prefix-op = (instof ?module:#ch/type!) :
         compile(InstanceExp(module))

   ;Circuit Statement
   defrule :
      prefix-op = (circuit ?name:#id! of ?module:#ch/type!) :
         compile(CircuitStmt(name, module))

   ;When Statement
   defrule when :
      when-exp = (when ?pred:#exp! #:! ?conseq:#exp! else : ?alt:#exp!) :
         compile(WhenStmt(pred, conseq, alt))
      when-exp = (when ?pred:#exp! #:! ?conseq:#exp! else ?alt:#when-exp) :
         compile(WhenStmt(pred, conseq, alt))
      when-exp = (when ?pred:#exp! #:! ?conseq:#exp!) :
         compile(WhenStmt(pred, conseq, false))
      prefix-op = (?e:#when-exp) :
         e         

   ;Connect Statement
   defrule :
      eop0 = (:= ?y:#exp!) :
         fn (x) :
            compile(ConnectStmt(x, y))

   ;Reset Statement
   defrule :
      prefix-op = (on-reset ?x:#e0 := ?y:#exp!) :
         compile(OnResetStmt(x, y))
      prefix-op != (on-reset) :
         CPE(form, "Connect statement expected after on-reset statement.")

   ;Not expression
   defrule :
      prefix-op = (~ ! ?e:#exp!) :
         expande $ qquote(chipper/bit-not(~ e))

   ;Bit Comparison Operator
   defrule :
      eop0 = (=== ?y:#exp!) :
         fn (x) :
            expande $ qquote(chipper/bit-equal?(~ x, ~ y))
      eop0 = (!== ?y:#exp!) :
         fn (x) :
            expande $ qquote(chipper/bit-not-equal?(~ x, ~ y))

   ;Dot Syntax for Field Access
   defrule :
      suffix-op = (. ?y:#id!) :
         fn (x) :
            val yname = to-symbol("field#~" << [y])
            qquote($do ~ yname ~ x)

   ;Syntax for Escaping to Type World
   defrule :
      prefix-op = (~ #{?t:#ch/type$}) :
         expande $ compile-to-type-exp(t)

   ;Literal Syntax
   defrule :
      prefix-op = (Bits(?v:#exp$)) :
         expande $ qquote(chipper/UIntLit(~ v))
      prefix-op = (Bits<?w:#exp$>(?v:#exp$)) :
         expande $ qquote(chipper/UIntLit(~ v, ~ w))
      prefix-op = (UInt(?v:#exp$)) :
         expande $ qquote(chipper/UIntLit(~ v))
      prefix-op = (UInt<?w:#exp$>(?v:#exp$)) :
         expande $ qquote(chipper/UIntLit(~ v, ~ w))
      prefix-op = (SInt(?v:#exp$)) :
         expande $ qquote(chipper/SIntLit(~ v))
      prefix-op = (SInt<?w:#exp$>(?v:#exp$)) :
         expande $ qquote(chipper/SIntLit(~ v, ~ w))
            
;============================================================

;================= CONTINUING EXPANSION =====================      
defn expande (form) :
   with-syntax(core) :
      match-syntax(form) :
         (?e:#exp) : e
;============================================================

;================== TYPE VALIDITY ===========================
defn statically-known? (t:Type) :
   match(t) :
      (t:UIntT|UIntWT|SIntT|SIntWT|ParamT) : true
      (t:VecT) : statically-known?(type(t))
      (t:NamedT) : all?(statically-known?, targs(t))
      (t:EscapeT) : false
;============================================================

;================= TYPE COMPILATION =========================
defn compile-to-type-exp (t:Type) :
   match(t) :
      (t:UIntT) :
         qquote(chipper/UInt())
      (t:UIntWT) :
         qquote(chipper/UInt(~ (width(t))))
      (t:SIntT) :
         qquote(chipper/SInt())
      (t:SIntWT) :
         qquote(chipper/SInt(~ (width(t))))
      (t:VecT) :
         fill-template(`(chipper/Vec(t, len)), [
            `t => compile-to-type-exp(type(t))
            `len => length(t)])
      (t:NamedT) :
         fill-template(`(name(ts, args)), [
            `name => name(t)
            `ts => splice(map(compile-to-type-exp, targs(t)))
            `args => splice(args(t))])
      (t:ParamT) :
         name(t)
      (t:EscapeT) :
         exp(t)

defn compile-to-type (t:Type) :
   match(t) :
      (t:UIntT|UIntWT) :
         qquote(chipper/UInt)
      (t:SIntT|SIntWT) :
         qquote(chipper/SInt)
      (t:VecT) :
         fill-template(`(chipper/Vec<T>), [
            `T => compile-to-type(type(t))])
      (t:NamedT) :
         fill-template(`(name<Ts>), [
            `name => name(t)
            `Ts => splice(map(compile-to-type, targs(t)))])
      (t:ParamT) :
         name(t)
      (t:EscapeT) :
         error("Cannot calculate type from EscapeT")
;============================================================

;================= BUNDLE COMPILATION =======================
defn compile (b:DefBundle) :
   val template = `((
      ;Handle Class Definition
      definterface A<targs{T}> <: parent
      targs{
         inherit{}{defmulti field#T<targs{?T}> (a:A<targs{?T}>) -> chipper/Type<T&bound>}
      }
      fields{
         inherit{}{defmulti field#name<targs{?T}> (a:A<targs{?T}>) -> type}
      }
      defclass AClass<targs{T}> <: A<targs{T}>
      defn AHandle<targs{T}> (targs{T:chipper/Type<T&bound>}, fields{ft:chipper/Type<type>}, e:firrtl/ir/Expression) :
         new AClass<targs{T}> :
            defmethod chipper/firrtl-exp (this) : e
            defmethod chipper/type (this) :
               AType<targs{T}>(targs{T}, fields{ft}) as chipper/Type<AClass<targs{T}>>
            targs{
               defmethod field#T (this) : T}
            fields{
               defmethod field#name (this) :
                  val e = firrtl/ir/Subfield(e, `name, firrtl/ir/UnknownType())
                  chipper/handle(ft, e)}

      ;Type Class Definition
      defclass AType<targs{T}> <: chipper/Type<A>
      defn AType<targs{T}> (targs{T:chipper/Type<T&bound>},
                            fields{ft:chipper/Type<type>})
                         -> chipper/Type<A<targs{T}>> :
         new AType<targs{T}> :               
            defmethod chipper/handle (this, e:firrtl/ir/Expression) :
               AHandle<targs{T}>(targs{T}, fields{ft}, e)
               
            defmethod chipper/firrtl-type (this) :
               firrtl/ir/BundleType $ to-list $ [fields{
                  firrtl/ir/Field(`name,
                     flip{firrtl/ir/REVERSE}{firrtl/ir/DEFAULT},
                     chipper/firrtl-type(ft))}]

            defmethod chipper/erase-width (this) :
               AType<targs{T}>(targs{chipper/erase-width(T)}, fields{chipper/erase-width(ft)})

      ;Constructor Definition
      defn A<targs{?T}> (targs{T:chipper/Type<?T&bound>}, args) :
         AType<targs{T}>(targs{T}, fields{type-exp})))

   val r = fill-template(template, [
      `A => name(b)
      `AClass => gensym(name(b))
      `AHandle => gensym(name(b))
      `AType => gensym(name(b))
      `a => gensym(`a)
      `e => gensym(`e)
      `parent => parent(b)
      `args => splice(arglist(b))
      `targs => nested $ for v in targs(b) stream : [
         `field#T => to-symbol("field#~" << [name(v)])
         `T => name(v)
         `inherit => choice(inherit(v))
         `bound => bound(v)]
      `fields => nested $ for p in fields(b) stream : [
         `ft => gensym(`T)
         `name => name(p)
         `flip => choice(flip(p))
         `inherit => choice(inherit(p))
         `field#name => to-symbol("field#~" <<  [name(p)])
         `type => compile-to-type(type(p))
         `type-exp => compile-to-type-exp(type(p))]])
         
   println("=== Expanding Bundle ===")
   println(r)
   expande(r)
;============================================================

;================ DEFMODULE COMPILATION =====================
defn compile (b:DefModule) :
   val template = `((
      ;Handle Class Definition
      definterface A<targs{T}> <: parent
      targs{
         inherit{}{defmulti field#T<targs{?T}> (a:A<targs{?T}>) -> chipper/Type<T&bound>}
      }
      ports{
         inherit{}{defmulti field#name<targs{?T}> (a:A<targs{?T}>) -> type}
      }
      params{
         inherit{}{defmulti field#x<targs{?T}> (a:A<targs{?T}>) -> type}
      }
      defclass AClass<targs{T}> <: A<targs{T}>
      defn AHandle<targs{T}> (targs{T:chipper/Type<T&bound>}, ports{pt:chipper/Type<type>}, params{x:type}, eval-body: () -> ?, e:firrtl/ir/Expression) :
         new AClass<targs{T}> :
            defmethod chipper/firrtl-exp (this) : e
            defmethod chipper/type (this) :
               AType<targs{T}>(targs{T}, ports{pt}, params{x}, eval-body) as chipper/Type<AClass<targs{T}>>
            targs{
               defmethod field#T (this) : T}
            ports{
               defmethod field#name (this) :
                  val e = firrtl/ir/Subfield(e, `name, firrtl/ir/UnknownType())
                  chipper/handle(pt, e)}
            params{
               defmethod field#x (this) : x}

      ;Type Class Definition
      defclass AType<targs{T}> <: chipper/Type<A> & chipper/CHModule
      defn AType<targs{T}> (targs{T:chipper/Type<T&bound>},
                            ports{pt:chipper/Type<type>},
                            params{x:type},
                            eval-body: () -> ?)
                         -> chipper/Type<A<targs{T}>> & chipper/CHModule :
         new AType<targs{T}> :
            defmethod chipper/firrtl-module (this) :
               val fs = to-list $ [ports{
                  firrtl/ir/Port(`name,
                     flip{firrtl/ir/INPUT}{firrtl/ir/OUTPUT},
                     chipper/firrtl-type(pt))}]
               chipper/ModuleExp(`A, fs, eval-body)
               
            defmethod chipper/handle (this, e:firrtl/ir/Expression) :
               AHandle<targs{T}>(targs{T}, ports{pt}, params{x}, eval-body, e)
               
            defmethod chipper/firrtl-type (this) :
               firrtl/ir/BundleType $ to-list $ [ports{
                  firrtl/ir/Field(`name,
                     flip{firrtl/ir/REVERSE}{firrtl/ir/DEFAULT},
                     chipper/firrtl-type(pt))}]

            defmethod chipper/erase-width (this) :
               AType<targs{T}>(targs{chipper/erase-width(T)}, ports{chipper/erase-width(pt)}, params{x}, eval-body)

      ;Constructor Definition
      defn A<targs{?T}> (targs{T:chipper/Type<?T&bound>}, args) :
         ports{val pt = type-exp}
         params{val x:type = exp}
         defn eval-body () :
            ports{val name = chipper/handle(pt, firrtl/ir/Ref(`name, firrtl/ir/UnknownType()))}
            body
         AType<targs{T}>(targs{T}, ports{pt}, params{x}, eval-body)))

   val r = fill-template(template, [
      `A => name(b)
      `AClass => gensym(name(b))
      `AHandle => gensym(name(b))
      `AType => gensym(name(b))
      `a => gensym(`a)
      `e => gensym(`e)
      `fs => gensym(`fs)      
      `eval-body => gensym(`eval-body)
      `parent => parent(b)
      `args => splice(arglist(b))
      `body => body(b)
      `targs => nested $ for v in targs(b) stream : [
         `field#T => to-symbol("field#~" << [name(v)])
         `T => name(v)
         `inherit => choice(inherit(v))
         `bound => bound(v)]
      `ports => nested $ for p in ports(b) stream : [
         `pt => gensym(`T)
         `name => name(p)
         `flip => choice(input?(p))
         `inherit => choice(inherit(p))
         `field#name => to-symbol("field#~" <<  [name(p)])
         `type => compile-to-type(type(p))
         `type-exp => compile-to-type-exp(type(p))]
      `params => nested $ for p in params(b) stream : [
         `x => name(p)
         `field#x => to-symbol("field#~" << [name(p)])
         `inherit => choice(inherit(p))
         `type => type(p)
         `exp => exp(p)]])
   println("=== Expanding Module ===")
   println(r)
   expande(r)
;============================================================

;================= REGISTER COMPILATION =====================
defn compile (s:RegisterStmt) :
   val template = `(
      val name = chipper/RegisterExp(`name, type))
   expande $ fill-template(template, [
      `name => name(s)
      `type => compile-to-type-exp(type(s))])

defn compile (s:RegisterFromExp) :
   val template = `(
      val name = chipper/RegisterFromExp(`name, exp))
   expande $ fill-template(template, [
      `name => name(s)
      `exp => exp(s)])
      
defn compile (s:RegisterResetFromExp) :
   val template = `(
      val name = chipper/RegisterResetFromExp(`name, exp))
   expande $ fill-template(template, [
      `name => name(s)
      `exp => exp(s)])
      
defn compile (s:RegisterExp) :
   val template = `(
      chipper/RegisterExp(`reg, type))
   expande $ fill-template(template, [
      `type => compile-to-type-exp(type(s))])
;============================================================

;================= WIRE COMPILATION =========================
defn compile (s:WireStmt) :
   val template = `(
      val name = chipper/WireExp(`name, type))
   expande $ fill-template(template, [
      `name => name(s)
      `type => compile-to-type-exp(type(s))])

defn compile (s:WireFromExp) :
   val template = `(
      val name = chipper/WireFromExp(`name, exp))
   expande $ fill-template(template, [
      `name => name(s)
      `exp => exp(s)])

defn compile (s:WireExp) :
   val template = `(
      chipper/WireExp(`w, type))
   expande $ fill-template(template, [
      `type => compile-to-type-exp(type(s))])
;============================================================

;================= NODE COMPILATION =========================
defn compile (s:NodeStmt) :
   val template = `(
      val name = chipper/NodeExp(`name, x))
   expande $ fill-template(template, [
      `name => name(s)
      `x => x(s)])
;============================================================

;================= Mem COMPILATION =========================
defn compile (s:SMemStmt) :
   val template = `(
      val name = chipper/SMemExp(`name, type))
   expande $ fill-template(template, [
      `name => name(s)
      `type => compile-to-type-exp(type(s))])
defn compile (s:CMemStmt) :
   val template = `(
      val name = chipper/CMemExp(`name, type))
   expande $ fill-template(template, [
      `name => name(s)
      `type => compile-to-type-exp(type(s))])
;============================================================

;================== ONRESET COMPILATION =====================
defn compile (s:OnResetStmt) :
   val template = `(
      chipper/OnResetExp(x, y))
   expande $ fill-template(template, [
      `x => x(s)
      `y => y(s)])
;============================================================

;=================== INSTANCE COMPILATION ===================
defn compile (s:InstanceStmt) :
   val template = `(
      val name = chipper/InstanceExp(`name, module))
   expande $ fill-template(template, [
      `name => name(s)
      `module => compile-to-type-exp(module(s))])

defn compile (s:InstanceExp) :
   val template = `(
      chipper/InstanceExp(`inst, module))
   expande $ fill-template(template, [
      `module => compile-to-type-exp(module(s))])
;============================================================

;================== CIRCUIT COMPILATION =====================
defn compile (s:CircuitStmt) :
   val template = `(
      val name = chipper/CircuitExp(module))
   expande $ fill-template(template, [
      `name => name(s)
      `module => compile-to-type-exp(module(s))])
;============================================================

;================ CONNECT COMPILATION =======================
defn compile (s:ConnectStmt) :
   val template = `(chipper/ConnectExp(x, y))
   expande $ fill-template(template, [
      `x => x(s)
      `y => y(s)])
;============================================================

;================== WHEN COMPILATION ========================
defn compile (s:WhenStmt) :
   val template = `(chipper/WhenExp(pred,
                       fn () : conseq
                       alt?{fn () : alt}{}))
   expande $ fill-template(template, [
      `pred => pred(s)
      `conseq => conseq(s)
      `alt? => choice(alt(s) != false)
      `alt => alt(s)])
;============================================================


;================== STANDALONE TESTING ======================
#if-defined(STANDALONE-CHIPPER) :
   defn main () :
      val lexed = lex-file(commandline-arguments()[1])
      with-syntax(core, chipper) :
         match-syntax(lexed) :
            (?bs:#exp ...) :
               do(println, bs)
   main()
;============================================================
